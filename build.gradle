plugins {
    id 'idea'
    id 'application'
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id 'com.github.johnrengelman.shadow' version '6.1.0' apply false
}

idea {
    project {
        languageLevel = 17
    }
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

allprojects {
    group 'ru.sladkov'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    apply plugin: 'java'
    compileJava {options.encoding = "UTF-8"}

    apply plugin: 'io.spring.dependency-management'
    dependencyManagement {
        dependencies {
            imports {
                mavenBom("org.springframework.boot:spring-boot-dependencies:2.6.4")
                mavenBom("com.google.protobuf:protobuf-bom:3.21.1")
            }
            dependency("com.google.guava:guava:${Versions.guava}")
            dependency("org.testcontainers:junit-jupiter:${Versions.testcontainers}")
            dependency("org.testcontainers:postgresql:${Versions.testcontainers}")
            dependency("io.grpc:grpc-netty:${Versions.grpc}")
            dependency("io.grpc:grpc-protobuf:${Versions.grpc}")
            dependency("io.grpc:grpc-stub:${Versions.grpc}")
        }
    }

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
        }
        resolutionStrategy {
            force("net.java.dev.jna:jna:5.8.0")
        }
    }
}